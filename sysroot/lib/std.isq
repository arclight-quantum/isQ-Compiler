extern defgate Rz(double) : gate(1) = "__quantum__qis__rz__body";
extern defgate Rx(double) : gate(1) = "__quantum__qis__rx__body";
extern defgate Ry(double) : gate(1) = "__quantum__qis__ry__body";
extern defgate U3(double, double, double) : gate(1) = "__quantum__qis__u3";
extern defgate H() : gate(1) = "__quantum__qis__h__body";
extern defgate S() : gate(1) = "__quantum__qis__s__body";
extern defgate T() : gate(1) = "__quantum__qis__t__body";
extern defgate X() : gate(1) = "__quantum__qis__x__body";
extern defgate Y() : gate(1) = "__quantum__qis__y__body";
extern defgate Z() : gate(1) = "__quantum__qis__z__body";
extern defgate CNOT() : gate(2) = "__quantum__qis__cnot";
extern defgate Toffoli() : gate(3) = "__quantum__qis__toffoli";
extern defgate X2M() : gate(1) = "__quantum__qis__x2m";
extern defgate X2P() : gate(1) = "__quantum__qis__x2p";
extern defgate Y2M() : gate(1) = "__quantum__qis__y2m";
extern defgate Y2P() : gate(1) = "__quantum__qis__y2p";
extern defgate CZ() : gate(2) = "__quantum__qis__cz";
extern defgate GPhase(double) : gate(0) = "__quantum__qis__gphase";

procedure SWAP(qbit a, qbit b) {
    CNOT(a, b);
    CNOT(b, a);
    CNOT(a, b);
} deriving gate

/*
* Follow paper: Y. Takahashi, S. Tani, and N. Kunihiro, "Quantum Addition 
* Circuits and Unbounded Fan-Out." arXiv preprint 0910.2530, 2009.
*/
procedure __add(qbit a[], qbit b[])
{
    int len = a.length;
    CNOT(a[1:len], b[1:]);
    CNOT(a[len-2:0:-1], a[len-1:0:-1]);
    Toffoli(a[0:], b, a[1:len]);
    for i in len-1:0:-1 {
        CNOT(a[i], b[i]);
        Toffoli(a[i - 1], b[i - 1], a[i]);
    }
    CNOT(a[1:], a[2:len]);
    CNOT(a, b);
}

procedure __sub(qbit a[], qbit b[])
{
    int len = a.length;
    CNOT(a, b);
    CNOT(a[len-2:0:-1], a[len-1:-1:-1]);
    for i in 1 : len {
        Toffoli(a[i - 1], b[i - 1], a[i]);
        CNOT(a[i], b[i]);
    }
    Toffoli(a[len-2:-1:-1], b[len-2:-1:-1], a[len-1:0:-1]);
    CNOT(a[1:], a[2:len]);
    CNOT(a[1:len], b[1:]);
}

procedure set(qbit q[], int val)
{
    for i in 0:q.length {
        if (((val >> i) & 1) == 1) {
            X(q[i]);
        }
    }
}
