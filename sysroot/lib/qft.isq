import std;

procedure R(int k, qbit q) {
    double phase = pi / 2 ** (k - 1);
    ctrl GPhase(phase, q);
} deriving gate

procedure qft(qbit q[]) {
    int len = q.length;
    for i in len-1:-1:-1 {
        H(q[i]);
        for j in 0:i {
            ctrl R(i-j+1, q[j], q[i]);
        }
    }
    for i in 0:len/2 {
        SWAP(q[i], q[len-i-1]);
    }
}

// The inverse of qft
procedure qft_inv(qbit q[]) {
    int len = q.length;
    for i in 0:len/2 {
        SWAP(q[i], q[len-i-1]);
    }
    for i in 0:len {
        for j in 0:i {
            inv ctrl R(i-j+1, q[j], q[i]);
        }
        H(q[i]);
    }
}

procedure main()
{
    qbit p[2], q;
    H(p[0]);
    CNOT(p[0], p[1]);
    H(q);
    ctrl R(1, p[0], q);
    H(q);
    bp;
}
