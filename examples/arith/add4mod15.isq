package arith;

import std;
import arith.add_const;
import arith.carrier;

procedure add4mod15(qbit a0, qbit a1, qbit a2, qbit a3) {
    qbit b0, b1, b2, b3, c;

    // set b = |5>
    X(b2);
    X(b0);

    // Test whether a + 4 >= 15
    carrier4(a0, a1, a2, a3, b0, b1, b2, b3, c);

    // If true, add 4 then subtract 15, i.e., subtract 11, i.e., add 5
    ctrl add5(c, a0, a1, a2, a3);

    // If false, add 4 directly
    X(c);
    ctrl add4(c, a0, a1, a2, a3);

    // Uncompute
    X(b0);
    X(b3);
    carrier4(a0, a1, a2, a3, b0, b1, b2, b3, c);
    X(b2);
    X(b3);
} deriving gate

procedure main()
{
    qbit q[4];
    for i in 0:16 {
        set(q, i);
        add4mod15(q[0], q[1], q[2], q[3]);
        print M(q);
        inv add4mod15(q[0], q[1], q[2], q[3]);
        set(q, i);
        print M(q);
    }
}