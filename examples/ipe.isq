defgate U = [0.9999916252719028-0.0040926013803331j, 0; 0, 1];  

/* 
U = [exp(i2PI * (0b11111111110101010101 / 2 ^ 20)), 0; 0, 1]
0b11111111110101010101 / 2^20 = 0.9993486404418945
*/

procedure pow2_ctrlU(int n, qbit anc, qbit ev){
    if(n < 0) { return; }
    if(n == 0) { ctrl U(anc, ev); return; }
    pow2_ctrlU(n - 1, anc, ev);
    pow2_ctrlU(n - 1, anc, ev);
}

double iterative_phase_estimation_U(int n, qbit ev){
    double PI = 3.1415926535;
    qbit anc;
    double phi = 0;
    for i in 0:n{
        H(anc);
        pow2_ctrlU(n - i - 1, anc, ev);
        Rz(- phi * 2 * PI, anc);
        H(anc);
        int res = M(anc);
        print res;
        phi = (phi + (res / 2.0)) / 2.0;
        anc = |0>;
    }
    return (phi * 2.0);
}

procedure main(){
    qbit ev; 
    ev = |0>; /* eigen vector of U */ 
    double phi = iterative_phase_estimation_U(20, ev);
    print phi;
}
